#!/usr/bin/env rune

# by Kevin Reid
# XXX Need copyright notice

# ? rune(["~/e/src/esrc/scripts/test/updoc/pass-by-construction.updoc"])

    ? pragma.syntax("0.9")

    ? def seedVat := <elang:interp.seedVatAuthor>(<unsafe>)
    ? def vsv := seedVat.virtualize((introducer.onTheAir(); introducer))
    # value: <virtualSeedVat>

    ? def [otherEval, vat] :=
    >   vsv("fn src { e__quasiParser(src).eval(safeScope) }")
    # value: [<Promise>, \
    #         <Vat newVirtualSeedVat in <runs in newVirtualSeedVat>>]

    ? def other := otherEval <- ("fn x { x }")

    ? def obj implements pbc { to __optUncall() :any {
    >   return [<elib:tables.makeFlexList>, "make", []]
    > }}
    # value: <obj>

    ? Ref.isPBC(obj)
    # value: true

    ? Ref.isPassByCopy(obj)
    # value: false

    ? obj :pbc
    # value: <obj>

    ? def r := interp.waitAtTop(other <- (obj))
    ? r
    # value: <Far ref>

    ? r <- push(1)
    # value: <Remote Promise>

    ? r <- push(2)
    # value: <Remote Promise>

    ? def s := interp.waitAtTop(r <- snapshot())
    ? s
    # value: [1, 2]

Odd case: maker is near only on receipt

    ? def otherMaker := interp.waitAtTop(
    >     otherEval <- ("fn { def oddFar {} }"))
    ? otherMaker
    # value: <Far ref>

    ? def r := interp.waitAtTop(
    >     other <- (
    >         def oddNear implements pbc {
    >             to __optUncall() {
    >                 return [otherMaker, "run", []] } }))
    ? r
    # value: <Far ref>

    ? def otherCall := otherEval <- ("fn r,v,a { E.call(r,v,a) }")
    # value: <Remote Promise>

    ? def t := interp.waitAtTop(otherCall <- (E, "toString", [r])); null
    ? t
    # value: "<oddFar>"

Auditing details

    ? Ref.isPBC(def notHonestlyPBC implements pbc {})
    # value: true

Miscellaneous

    ? def pbcs := [
    >     0,
    >     ' ',
    >     false,
    >     "" :<type:java.lang.Throwable>,
    >     makeSturdyRef.temp(1),
    >     term__quasiParser.valueMaker("1"),
    >     <import:java.lang.makeInteger>,
    >     <import:java.lang.makeInteger> :<type:java.lang.Runnable>,
    >     <elib:base.makeValueThunk>(null),
    >     [].asMap(),
    >     [].asSet(),
    >     [],
    >     <import>
    > ]
    > for i => thing in pbcs {
    >     if (!Ref.isPBC(thing)) {
    >         println(`#$i should be PBC: `,
    >                 thing,
    >                 " ",
    >                 thing.__getAllegedType().getFQName())
    >     }
    > }
    > print("of ", pbcs.size())
    # stdout: of 13

    ? def notpbcs := [
    >     def _ {},
    >     def _ {} :<type:java.lang.Runnable>,
    > ]
    > for i => thing in notpbcs {
    >     if (Ref.isPBC(thing)) {
    >         println(`#$i shouldn't be PBC: `,
    >                 thing,
    >                 " ",
    >                 thing.__getAllegedType().getFQName())
    >     }
    > }
    > print("of ", notpbcs.size())
    # stdout: of 2
