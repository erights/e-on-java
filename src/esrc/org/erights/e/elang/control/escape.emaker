#!/usr/bin/env rune

# Copyright 2005 Mark S. Miller, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")
pragma.enable("explicit-result-guard")

/**
 * Unlike many of the other example controls, this one is implemented in terms
 * of its corresponding special form.
 */
def __escape0 {
    to fn__control_1ej(try1Block) :any {
        def escapeControl {
            to run__control() :any {
                return escape ejector {
                    try1Block(ejector, null)
                }
            }
            to catch__control_1ej(handler1Block) :any {
                def escapeCatchControl {
                    to run__control() :any {
                        return escape ejector {
                            try1Block(ejector, null)
                        } catch problem {
                            handler1Block(problem, null)
                        }
                    }
                }
                return escapeCatchControl
            }
        }
        return escapeControl
    }
}
