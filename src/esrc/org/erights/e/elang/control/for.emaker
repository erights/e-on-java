#!/usr/bin/env rune

# Copyright 2004 Mark S. Miller, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")
pragma.enable("explicit-result-guard")
pragma.enable("one-method-object")

def makeSeries := <elang:control.makeSeries>

/**
 * Just handles some simple for-loop-like cases.
 * <p>
 * If you want to do something complex, use makeSeries instead, or as the
 * collection argument of a ::"for"
 */
def __for0 {
    to each__control_1_2ej(compoundFn) :any {
        def each2Control.run__control() :void {
            def [[coll], each2Block] := compoundFn()
            makeSeries(coll).each2(each2Block)
        }
        return each2Control
    }
    to each__control_1_1ej(compoundFn) :any {
        def each1Control.run__control() :void {
            def [[coll], each1Block] := compoundFn()
            makeSeries(coll).each(each1Block)
        }
        return each1Control
    }
    to map__control_1_2ej(compoundFn) :any {
        def map2Control.run__control() :List {
            def [[coll], map2Block] := compoundFn()
            return makeSeries(coll).map2(map2Block).asList()
        }
        return map2Control
    }
    to map__control_1_1ej(compoundFn) :any {
        def map1Control.run__control() :List {
            def [[coll], map1Block] := compoundFn()
            return makeSeries(coll).map(map1Block).asList()
        }
        return map1Control
    }
    to filter__control_1_1ej(compoundFn) :any {
        def filter1Control.run__control() :List {
            def [[coll], filter1Block] := compoundFn()
            return makeSeries(coll).filter(filter1Block).asList()
        }
        return filter1Control
    }
    to fold__control_2_2ej(compoundFn) :any {
        def foldControl.run__control() :any {
            def [[seed,coll], fold2Block] := compoundFn()
            return makeSeries(coll).fold(seed, fold2Block)
        }
        return foldControl
    }
    to splice__control_1_1ej(compoundFn) :any {
        def spliceControl.run__control() :any {
            def [[coll], iter1Block] := compoundFn()
            return makeSeries(coll).splice(iter1Block).asList()
        }
        return spliceControl
    }
}
