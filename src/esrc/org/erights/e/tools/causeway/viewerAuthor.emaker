#!/usr/bin/env rune

pragma.syntax("0.8")

# Copyright 2002 Combex, Inc. under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

def swt__uriGetter := <import:org.eclipse.swt.*>
def widget__uriGetter := <swt:widgets.*>
def custom__uriGetter := <swt:custom.*>
def SWT := <swt:makeSWT>

def <swttools> := <import:org.erights.e.ui.swt.*>
def swtGrid__quasiParser := <swttools:swtGridQParserMaker>()

def stackWalkerAuthor :=
  <import:org.erights.e.tools.causeway.stackWalkerAuthor>

def treePaneAuthor :=
  <import:org.erights.e.tools.causeway.treePaneAuthor>

def poTreePaneAuthor :=
  <import:org.erights.e.tools.causeway.poTreePaneAuthor>

def debugPaneAuthor :=
  <import:org.erights.e.tools.causeway.debugPaneAuthor>

def getDisplayName(vatName) :String {
    
    # find most specific folder name
    def parts := vatName.split("/")
    var i := parts.size() -1
    while (i >= 0) {
        def part := parts[i]
        if (part != "") { return part }
        i -= 1
    }
    return vatName
}

def makeVatMap(vats, palette, traceline) :near {
    
    def attributes := [].asMap().diverge()
    
    for i in 0 ..! vats.size() {
        def name := getDisplayName(vats[i])
        def color := palette.getVatColor(i)
        attributes.put([vats[i]],[name, color], true)
    }
    def vatMap {
        to fetchDisplayAttributes(vat) :near {
            return attributes.fetch([vat], fn{
                [vat, palette.getDefaultVatColor()]
            })
        }
    }
    return vatMap
}

def makeFolderGroup(folder, palette, traceline) :near {
    
    def initialSelection := [].diverge()
    def tabKeeper := [].diverge()
    
    def folderGroup {
        
        to add(tabbedPane, label, selected) :void {
            def tab := <widget:makeTabItem>(folder, 0)
            
            tab.setControl(tabbedPane.getPane())
            tab.setText(label)
            tabKeeper.push([tab, tabbedPane])
            
            if (selected == true) {
                initialSelection.push(tab)
            }
        }
        
        to layout() :void {
            for [_, tabbedPane] in tabKeeper {
                tabbedPane.layout()
            }
        }
        
        to open() :void {
            #if (tabKeeper == []) { return }
            #def ndx := folder.indexOf(initialSelection[0])
            #if (ndx != -1 ) {
            #    folder.setSelection(ndx)
            #}
            #for t in tabKeeper {
            #    def [tab, tabbedPane] := t
            #    if (tab == initialSelection[0]) {
            #        tabbedPane.activate()
            #        return
            #    }
            #}
        }
    }
    return folderGroup
}


/**
 */
def viewerAuthor(<file>, <jar>, makers, shell, tcr, traceline) :near {
    
    def makeViewer(parent, palette) :near {
        
        def messageGraph
        
        def topToBottom := <custom:makeSashForm>(parent, SWT.getVERTICAL())
        def sideToSide := <custom:makeSashForm>(topToBottom, SWT.getHORIZONTAL())
        
        def fp := palette.makeFolderPalette()
        
        # top-left
        def poTreeFolder := <widget:makeTabFolder>(sideToSide, 0)
        poTreeFolder.setForeground(fp.getFg())
        poTreeFolder.setBackground(fp.getBg())
        poTreeFolder.setFont(fp.getFont())
        def poTreeFolderGroup := makeFolderGroup(poTreeFolder, palette, traceline)
        def makePOTreePane := poTreePaneAuthor(traceline)
        
        # top-middle
        def treeFolder := <widget:makeTabFolder>(sideToSide, 0)
        treeFolder.setForeground(fp.getFg())
        treeFolder.setBackground(fp.getBg())
        treeFolder.setFont(fp.getFont())
        def treeFolderGroup := makeFolderGroup(treeFolder, palette, traceline)
        def makeTreePane := treePaneAuthor(traceline)
        
        # top-right
        def debugFolder := <widget:makeTabFolder>(sideToSide, 0)
        debugFolder.setForeground(fp.getFg())
        debugFolder.setBackground(fp.getBg())
        debugFolder.setFont(fp.getFont())
        def debugFolderGroup := makeFolderGroup(debugFolder, palette, traceline)
        def makeDebugPane := debugPaneAuthor(<file>, <jar>, traceline)
        
        # bottom
        def makeStackWalker := stackWalkerAuthor(<file>, <jar>, traceline)
        def stackWalker := makeStackWalker(topToBottom, makers, palette)
        
        topToBottom.setWeights([50, 50])
        sideToSide.setWeights([40, 35, 25])
        
        def viewer {
            
            to getTopLevelPane() :near { return topToBottom }
            
            to open() :void {
                debugFolderGroup.open()
                poTreeFolderGroup.open()
                treeFolderGroup.open()
                stackWalker.open()
            }
            
            to layout() :void {
                debugFolderGroup.layout()
                poTreeFolderGroup.layout()
                treeFolderGroup.layout()
                stackWalker.layout()
            }
            
            to setModel(model) :void {
                bind messageGraph := model.getTop()
                def vatSet := model.getVatSet()
                
                def vatMap := makeVatMap(vatSet, palette, traceline)
                
                def debugPane := makeDebugPane(debugFolder, makers, palette)
                debugFolderGroup.add(debugPane, debugPane.getTabLabel(), true)
                
                def treePane := makeTreePane(treeFolder, palette, messageGraph, vatMap, stackWalker, debugPane)
                treeFolderGroup.add(treePane, treePane.getTabLabel(), true)
                
                for i in 0 ..! vatSet.size() {
                    def poList := model.getOrdered(vatSet[i])
                    if (poList != []) {
                        def poTreePane := makePOTreePane(poTreeFolder, palette, poList,
                                                         vatSet[i], vatMap, treePane)
                        poTreeFolderGroup.add(poTreePane, poTreePane.getTabLabel(), i == 0)
                    }
                }
            }
        }
        return viewer
    }
}

