#!/usr/bin/env rune

pragma.syntax("0.9")

# Copyright 2009 Teleometry Design under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

def swt__uriGetter := <import:org.eclipse.swt.*>
def widget__uriGetter := <swt:widgets.*>
def custom__uriGetter := <swt:custom.*>
def SWT := <swt:makeSWT>
def makeConstMap := <elib:tables.makeConstMap>

def File := <type:java.io.File>

def <swttools> := <import:org.erights.e.ui.swt.*>
def swtGrid__quasiParser := <swttools:swtGridQParserMaker>()

def <causeway> := <import:org.erights.e.tools.causeway.*>
def makeSearchView := <causeway:makeSearchView>
def debugViewAuthor := <causeway:debugPaneAuthor>
def stackWalkerAuthor := <causeway:stackWalkerAuthor>
def makeMessageOrderView := <causeway:makeMessageOrderView>
def makeProcessOrderView := <causeway:makeProcessOrderView>
def makeFilterOptionsView := <causeway:makeFilterOptionsView>

def getDisplayName(vatName) :String {
    
    if (vatName =~ `<Vat @vn>`) {
        if (vn =~ `@_/-/@name`) {
            return name
        } else {
            return vn
        }
    }
    
    # find most specific folder name
    def parts := vatName.split("/")
    var i := parts.size() -1
    while (i >= 0) {
        def part := parts[i]
        if (part != "") { return part }
        i -= 1
    }
    return vatName
}

def makeVatMap(vats, palette, traceline) :near {
    
    def attributes := [].asMap().diverge()
    
    for i in 0 ..! vats.size() {
        def name := getDisplayName(vats[i])
        def color := palette.getVatColor(i)
        attributes.put([vats[i]],[name, color], true)
    }
    def vatMap {
        to fetchDisplayAttributes(vat) :near {
            return attributes.fetch([vat], fn{
                [vat, palette.getDefaultVatColor()]
            })
        }
    }
    return vatMap
}

/**
 */
def viewerAuthor(rootDirName :String,
                 <file>, <jar>, props,
                 makers, shell, abstractAction, tcr, traceline) :near {
    
    def makeStackWalker := stackWalkerAuthor(rootDirName,
                                             <file>,
                                             <jar>,
                                             makers,
                                             traceline)
    
    def optMakeDebugView := if (makeConstMap.testProp(props,
                                                      "causeway_debug")) {
        debugViewAuthor(<file>,
                        <jar>,
                        makers,
                        traceline)
    } else {
        null
    }
    
    def makeViewer(swtParent, palette, 
                   viewerModel, selectionModel, searchModel) :near {
        
        def model := viewerModel
        def messageGraph := model.getTop()
        
        def vatSet := model.getVatSet()
        def vatMap := makeVatMap(vatSet, palette, traceline)
        
        def swtViewerView := <widget:makeComposite>(swtParent, 0)
        
        # a little confusing...
        # visually, its a vertical sash, but its children span the horizontal
        def swtSearchDivider := <custom:makeSashForm>(swtViewerView,
                                                      SWT.getHORIZONTAL())
                                                      
        def stackWalker
        # left-most, full-length view, left of search divider, first child
        def searchView := makeSearchView(swtSearchDivider, palette,
                                         abstractAction,
                                         searchModel,
                                         selectionModel,
                                         vatMap, stackWalker)

        def swtTopToBottom := <custom:makeSashForm>(swtSearchDivider,
                                                    SWT.getVERTICAL())
        def swtSideToSide := <custom:makeSashForm>(swtTopToBottom,
                                                   SWT.getHORIZONTAL())
        
        # bottom
        bind stackWalker := makeStackWalker(swtTopToBottom, palette,
                                            abstractAction,
                                            selectionModel, 
                                            searchModel,
                                            vatMap)
        

        # Map [VatName, List[TurnNode]]
        def poModel := model.getPOMap()
        def pov := makeProcessOrderView(swtSideToSide, palette,
                                        abstractAction,
                                        poModel, 
                                        selectionModel, searchModel,
                                        vatMap, stackWalker)
        
        def mov := makeMessageOrderView(swtSideToSide, palette,
                                        abstractAction,
                                        messageGraph, 
                                        selectionModel, searchModel,
                                        vatMap, stackWalker)
        
        # top-right
        if (null == optMakeDebugView) {
            swtSideToSide.setWeights([35, 65])
        } else {
            def dv := optMakeDebugView(swtSideToSide, palette,
                                       abstractAction,selectionModel)
            
            swtSideToSide.setWeights([30, 55, 15])
            dv.layout()
        }
        # all views to the right of search divider
        swtTopToBottom.setWeights([60, 40])
        
        swtSearchDivider.setWeights([15, 85])
        
        stackWalker.layout()
        searchView.layout()
        pov.layout()
        mov.layout()
        
        def viewer {
            
            to getTopLevelSWTControl() :near { return swtViewerView }
            
            to open() :void {}
            
            to layout() :void {
            
                # layout kids starting with most nested
                def ssKids := swtSideToSide.getChildren()
                for ssKid in ssKids {
                    ssKid.layout()
                }
                def tbKids := swtTopToBottom.getChildren()
                for tbKid in tbKids {
                    tbKid.layout()
                }
                def sdKids := swtSearchDivider.getChildren()
                for sdKid in sdKids {
                    sdKid.layout()
                }

                swtGrid`${viewer.getTopLevelSWTControl()}:
                        ${swtSearchDivider}.X.Y`
            }
            
            to exportModelToDotFile(dotFile :File) :void {
                if (Ref.isNear(model)) {
                    # if message-order view has a selected item,
                    # start graph walk at that node
                    var root := mov.getSelectionModel()
                    if (root == null) {
                        # else, walk entire messageGraph
                        root := messageGraph
                    }
                    
                    model.exportToDotFile(root,
                                          dotFile, palette,
                                          vatMap, stackWalker)
                } else {
                    traceline("There is no model to export to dot file.")
                }
            }
            
            to findLostMessages() :void {
                searchModel.findLostMessages(model.getBottom())
            }
            
            to showFilterOptions() :any {
                
                def result
                
                def CANCEL := 0
                def SAVE := 1
                def UPDATE_VIEWER := 2
                
                def swtDialog := makers["modelessDialog"](shell, 
                                                          SWT.getSHELL_TRIM())
                def dialogPalette := palette.makeDialogPalette()
                swtDialog.setForeground(dialogPalette.getFg())
                swtDialog.setBackground(dialogPalette.getBg())
                swtDialog.setFont(dialogPalette.getFont())
                swtDialog.setText("Filter Options")
                
                def ffModel := model.getOrMakeFileFilterModel()
                def fov := makeFilterOptionsView(swtDialog,
                                                 palette,
                                                 ffModel)
                fov.layout()
                def swtFOV := fov.getTopLevelSWTControl()
                def fovSize := swtFOV.getSize()  # remember size
#                traceline(`fovSize: $\
#${fovSize.getX()}, $\
#${fovSize.getY()}`)

                def swtCancel := <widget:makeButton>(swtDialog,
                                                     SWT.getPUSH())
                swtCancel.setForeground(dialogPalette.getFg())
                swtCancel.setBackground(dialogPalette.getBg())
                swtCancel.setFont(dialogPalette.getFont())
                swtCancel.setText("Cancel")
                
                swtCancel.addSelectionListener(def swtListenerForCancel {
                    to widgetSelected(swtEvent) :void {
                        # revert file filter model to original
                        model.remakeFileFilterModel()
                        swtDialog.close()
                        swtDialog.dispose()
                        bind result := CANCEL
                    }
                    to widgetDefaultSelected(swtEvent) :void {}
                })
                
                def swtSave := <widget:makeButton>(swtDialog,
                                                   SWT.getPUSH())
                swtSave.setEnabled(false)  # not implemented yet
                swtSave.setForeground(dialogPalette.getFg())
                swtSave.setBackground(dialogPalette.getBg())
                swtSave.setFont(dialogPalette.getFont())
                swtSave.setText("Save")
                
                swtSave.addSelectionListener(def swtListenerForSave {
                    to widgetSelected(swtEvent) :void {
                        # remake file filter model from current model
                        # to cleanly break connections between 
                        # model & observers
                        model.remakeFileFilterModel(ffModel)
                        swtDialog.close()
                        swtDialog.dispose()
                        bind result := SAVE
                    }
                    to widgetDefaultSelected(swtEvent) :void {}
                })
                
                def swtUpdateViewer := <widget:makeButton>(swtDialog,
                                                           SWT.getPUSH())
                swtUpdateViewer.setForeground(dialogPalette.getFg())
                swtUpdateViewer.setBackground(dialogPalette.getBg())
                swtUpdateViewer.setFont(dialogPalette.getFont())
                swtUpdateViewer.setText("Update Viewer")
                
               swtUpdateViewer.addSelectionListener(
                    def swtListenerForUpdateViewer {
                        to widgetSelected(swtEvent) :void {
                            # remake file filter model from current model
                            # to cleanly break connections between 
                            # model & observers
                            model.remakeFileFilterModel(ffModel)
                            swtDialog.close()
                            swtDialog.dispose()
                            bind result := UPDATE_VIEWER
                        }
                        to widgetDefaultSelected(swtEvent) :void {}
                    })
                
                swtDialog.setDefaultButton(swtUpdateViewer)

                def swtFiller := <widget:makeLabel>(swtDialog, 0)
                swtFiller.setForeground(dialogPalette.getFg())
                swtFiller.setBackground(dialogPalette.getBg())

                # swtGrid sets up the relative bounds of SWT display widgets
                # in this case,
                #   fov spans 4 columns and grabs available X & Y area;
                #   the last row contains the 3 push buttons which
                #   are right justified since filler grabs X
                
                swtGrid`${swtDialog}:
              $swtFOV.X.Y  >          >        >
              $swtFiller.X $swtCancel $swtSave $swtUpdateViewer`
                
                # for this dialog, set specific sizes to 
                # constrain the general layout defined by swtGrid
                
                swtDialog.pack()
                
                swtFOV.setSize(fovSize.getX(), fovSize.getY())  # restore size

                def [xe, ye] := dialogPalette.getCharExtent()

                var width := 0
                var height := ye *2  # just one row, plus a bit more

                swtFiller.setSize(0, ye)
                swtCancel.setSize((swtCancel.getText().size() +4) * xe, ye)
                swtSave.setSize((swtSave.getText().size() +4) * xe, ye)
                swtUpdateViewer.setSize(
                    (swtUpdateViewer.getText().size() +4) * xe, ye)
                    
                width += swtCancel.getSize().getX()
                width += swtSave.getSize().getX()
                width += swtUpdateViewer.getSize().getX()
                                
                width max= fovSize.getX()
                height += fovSize.getY()
                
                swtDialog.setSize(width, height)

#                palette.setMargins(swtDialog.getLayout(), 5, 10, 15, 10)
                
                swtDialog.open()
                return result
            }
        }
        return viewer
    }
    return makeViewer
}

