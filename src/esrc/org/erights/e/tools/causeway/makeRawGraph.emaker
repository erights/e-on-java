#!/usr/bin/env rune

pragma.syntax("0.8")

# Copyright 2009 Teleometry Design under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

def makeSourceSpan := <elib:base.makeSourceSpan>
def <causeway> := <import:org.erights.e.tools.causeway.*>
def makeMessageGraph := <causeway:makeMessageGraph>

# [[start-line, start-col], [end-line, end-col]]
# [[1, 0]] is the first character in a source file.
# start-col refers to the first character of interest.
# end-col refers to the last character of interest.
def SourceSpan := Tuple[Tuple[int,int], Tuple[int, int]]

# [filename, fncname]
def SourceName := Tuple[String, String]

# A [source-name, source-span]
def StackEntry := Tuple[SourceName, nullOk[SourceSpan]]

# top-to-bottom list of stack entries
def Stack := List[StackEntry]

def VatName := String

def Turn := int

def UTurn := Tuple[VatName, Turn]

# one of "org.ref_send.log.Sent", ...
def LogEvent := String

# Turn valid trace file spans into a fully-specified SourceSpan.
# For example,
# [ [ 5 ] ] ==>        [ [ 5, 0 ], [ 5, 0 ] ]
# [ [ 5 ], [ 6 ] ] ==> [ [ 5, 0 ], [ 6, 0 ] ]

def spanner(span) :SourceSpan {
    def term`[ [@{sline :int},
                @{scolL :List[int]}?], 
              ([@{elineL :List[int]}, 
                @{ecolLL :List[List[int]]}?])?]` := span
    return [[sline,
             if (scolL =~ [scol]) {scol} else {0}],
            [if (elineL =~ [eline]) {eline} else {sline},
             if (ecolLL =~ [[ecol]]) {ecol} else {
        if (scolL =~ [scol]) {scol} else {0}}]]
}

/**
 * <tt>makeRawGraph</tt> returns the messageGraph,
 * the top element for navigating the significant portion of that graph,
 * and the set of pathnames seen by the parse.
 */
def makeRawGraph(ttChunks,
                 swtProgressMsg,
                 swtProgressBar,
                 traceline) :Tuple[near, near, near] {
    
    # Map[MsgID, EventArc]
    def msgMap := [].asMap().diverge()
    
    # Messages := List[MsgID]
    # Map[Condition, Messages]
    def condMap := [].asMap().diverge()
    
    def resolveds := [].diverge()
    
    def graph := makeMessageGraph(traceline)
    
    # top has no incoming edges; bottom has no outgoing edges
    def top := graph.makeTurnNode(["top", 0])
    def bottom := graph.makeTurnNode(["bottom", 0])
    graph.addTurnNode(top)
    graph.addTurnNode(bottom)
    
    # set of source paths seen during parse
    var pathnames := [].asSet().diverge()
    
    # Parse term tree structure. Return E Stack object which could be empty.
    def parseStack(stack) :Stack {
        def entries := [].diverge()
        
        for stackFrame in stack {
            
            def term`{ "name": @{fnc :String},
                       "source": @{source :String},
                       ("span": @optSpan)?,
                       @_* 
                     }` := stackFrame
            def optSpanner := if (optSpan =~ [span]) {
                spanner(span)
            } else {
                null
            }
            entries.push([[source, fnc], optSpanner])
            # add to set of source paths seen during parse
            pathnames.addElement(source)
        }
        return entries.snapshot()
    }
    
    def parseOptStack(optStack) :Stack {
        switch (optStack) {
            match [] { return [] }
            match [stack] { return parseStack(stack) }
        }
    }
    
    # Return the node for the turn in the vat.
    # If the node does not exist yet, make it.
    def getOrMakeTurnNode(name :VatName, turn :Turn) :near {
        var node := graph.optGet([name, turn])
        if (node == null) {
            node := graph.makeTurnNode([name, turn])
            graph.addTurnNode(node)
        }
        return node
    }

    # Maps from elements to chunks
    def chunkMap := [].asMap().diverge()
    swtProgressMsg.setText(`Building message graph ...`)
    swtProgressBar.setMinimum(0)
    swtProgressBar.setMaximum(ttChunks.size() -1)
    
    for i => chunk in ttChunks {
        
        swtProgressBar.setSelection(i)

        # types is the list of type names of the current chunk, in order,
        # from most specific subtype to more general supertypes.
        def term`{"class": [@{types :List[String]}*], @_* }` := chunk
        def subtype := types[0]
        def subtypeName := subtype.split(".").last()
        
        var optSpan := null
        
        if (chunk =~ term`{@dollar: @_, @_*}`) {
            optSpan := dollar.getOptSpan()
            if (null != optSpan && i +1 < ttChunks.size()) {
                
                if (ttChunks[i +1] =~ term`{@nextDollar: @_, @_*}`) {
                    def nextOptSpan := nextDollar.getOptSpan()
                    if (null != nextOptSpan) {
                        optSpan := makeSourceSpan.optCover(optSpan,
                                                           nextOptSpan)
                    }
                }
            }
        }
        
        # A Sent event (or a subtype e.g., SentIf, Returned) and its
        # corresponding Got event match on message id.
        # If Sent is seen first, make an edge from this origin to a
        # place-holder target (bottom). Put the edge in the msgMap to
        # be fetched by the corresponding Got.
        # If Got is seen first, fetch the edge from the msgMap and
        # replace the place-holder origin (top) with this origin.
                
        # Causeway's Find Lost Messages command finds 
        # Sent events having no corresponding Got.
                
        # The edge has an event type and (optionally) a sending stack.
        # For example, a Sent event trace object is the stack capture
        # at the point of the message send.

        # Edges represent events occurring within a turn. The Sent
        # event inTurn value defines process order for the intra-turn
        # event. Got events are at top of turn and always have an
        # inTurn value of zero.
        
        # Messages can be transmitted multiple times when connections are 
        # transient or temporarily dropped, so we ignore duplicates.
        
        switch (chunk) {
            
            match term`{ "class": [ @_*,
                                     "org.ref_send.log.Sent",
                                     "org.ref_send.log.Event" ],
                             "anchor": {
                                 "number": @{inTurn :int},
                                 "turn": {
                                     "loop": @{name :String},
                                     "number": @{turn :int}
                                 }
                             },
                             ("condition": @{optCondition :List[String]})?,
                             "message": @{message :String},
                             ("timestamp": @{optTimestamp :List[int]})?,
                             ("text": @{comment :List[String]})?,
                             ("trace": { "calls": [@optStack*] })?,
                             @_*
                           }` {
                def origin := getOrMakeTurnNode(name, turn)
                
                # Create E Stack object, possibly empty, from term tree.
                def entries := parseOptStack(optStack)
                
                var edge := msgMap.fetch(message, fn{null})
                if (edge == null) {  # Sent event seen first
                    
                    edge := graph.makeEventArc(origin, bottom,
                                               subtype, entries)
                    edge.setInTurn(inTurn)
                    edge.addLogFile(optSpan)
                    msgMap[message] := edge  # Corresponding Got will fetch edge
                    
                } else {  # Got event seen first
                    
                    if (edge.getOrigin() == top) {
                        edge.setOrigin(origin)
                        edge.setContext(subtype, entries)
                        edge.setInTurn(inTurn)
                        edge.addLogFile(optSpan)
                        
                    } else if ((chunkMap.fetch(edge, fn{}) =~ 
                                  oldChunk :notNull) &&
                               chunk <=> oldChunk) {  # term tree compare
                        # ignore duplicate
                    } else {
                        traceline(
                            `origin conflict for $message ($subtypeName)`)
                    }
                }
                # If Sent event has timestamp, it belongs to the edge.
                if (optTimestamp =~ [timestamp]) {
                    edge.setOptTimestamp(timestamp)
                }
                
                # Remember messages associated with a promise for
                # hooking up Resolved events.
                if (optCondition =~ [condition]) {
                    # TODO assert that types must include SentIf
                    def messages := condMap.fetch(condition,
                                                  fn{[].diverge()})
                    messages.push(message)
                    condMap[condition] := messages
                }
                if (comment =~ [text]) {
                    edge.setOptText(text)
                }

                chunkMap[edge] := chunk  # remember for duplicate testing
            }
            match term`{ "class": [ @_*,
                                     "org.ref_send.log.Got",
                                     "org.ref_send.log.Event" ],
                             "anchor": {
                                 "number": @{inTurn :int},
                                 "turn": {
                                     "loop": @{name :String},
                                     "number": @{turn :int}
                                 }
                             },
                             "message": @{message :String},
                             ("timestamp": @{optTimestamp :List[int]})?,
                             ("trace": { "calls": [@optStack*] })?,
                             @_*
                           }` {
                
                def target := getOrMakeTurnNode(name, turn)
                
                var edge := msgMap.fetch(message, fn{null})
                if (edge == null) {  # Got event seen first
                    
                    edge := graph.makeEventArc(top, target,
                                               "unknown sender", [])
                    msgMap[message] := edge  # Corresponding Sent will fetch edge
                    
                } else {  # Sent event seen first
                    
                    if (edge.getTarget() == bottom) {
                        edge.setTarget(target)
                    } else if ((chunkMap.fetch(target, fn{}) =~ 
                                  oldChunk :notNull) &&
                               chunk <=> oldChunk) {  # term tree compare
                        # ignore duplicate
                    } else {
                        traceline(
                            `target conflict for $message ($subtypeName)`)
                    }
                }
                
                # Create E Stack object, possibly empty, from term tree.
                def entries := parseOptStack(optStack)
                target.setContext(subtype, entries)
                
                # If Got event has timestamp, it belongs to the node.
                if (optTimestamp =~ [timestamp]) {
                    target.setOptTimestamp(timestamp)
                }

                target.addLogFile(optSpan)
                
                chunkMap[target] := chunk # remember for duplicate testing
            }
            
            match term`{ "class": [ @_*,
                                     "org.ref_send.log.Resolved",
                                     "org.ref_send.log.Event" ],
                             @_*
                           }` {
                
                    # Resolved events are handled later
                resolveds.push([chunk, optSpan, subtype, subtypeName])
            }
            
            match term`{ "class": [ @_*,
                                     "org.ref_send.log.Comment",
                                     "org.ref_send.log.Event" ],
                             "anchor": {
                                 "number": @{inTurn :int},
                                 "turn": {
                                     "loop": @{name :String},
                                     "number": @{turn :int}
                                 }
                             },
                             ("text": @{comment :List[String]})?,
                             ("timestamp": @{optTimestamp :List[int]})?,
                             ("trace": { "calls": [@optStack*] })?,
                             @_*
                           }` {
                
                def origin := getOrMakeTurnNode(name, turn)
                
                # Create E Stack object, possibly empty, from term tree.
                def entries := parseOptStack(optStack)
                
                def edge := graph.makeEventArc(origin, bottom,
                                               subtype, entries)
                edge.setInTurn(inTurn)
                
                # If Comment event has timestamp, it belongs to the edge.
                if (optTimestamp =~ [timestamp]) {
                    edge.setOptTimestamp(timestamp)
                }

                if (comment =~ [text]) {
                    edge.setOptText(text)
                }

                edge.addLogFile(optSpan)
            }
            match _ {
                traceline(`no match: $chunk`)
            }
        }
    }
    
    # Now, handle the Resolved events, e.g., Fulfilled, Progressed, Rejected.
    # For example, a SentIf record is logged with the registration of a 
    # when-block. It is a conditional send, waiting on the resolution
    # of a promise. If a Resolved event is seen (matching on condition) the
    # message target has multiple causes: 1) the registration of the when-block 
    # and 2) the resolving of the promise.
    
    # Earlier parsing handled SentIf events as Sent events but remembered them
    # in the condMap. Here, we update the graph with the Resolved event
    # as a multiple cause of the message target.
    
    for [r, span, st, stn] in resolveds {
        if (r =~ term`{ "class": [ @_*,
                                "org.ref_send.log.Resolved",
                                "org.ref_send.log.Event" ],
                        "anchor": {
                            "number": @{inTurn :int},
                            "turn": {
                                "loop": @{name :String},
                                "number": @{turn :int}
                            }
                        },
                        "condition": @{condition :String},
                        ("timestamp": @{optTimestamp :List[int]})?,
                        ("text": @{comment :List[String]})?,
                        ("trace": { "calls": [@optStack*] })?,
                        @_*
                      }`) {
            
            def messages := condMap.fetch(condition, fn{null})
            if (messages != null) {
                
                def origin := getOrMakeTurnNode(name, turn)
                
                # Create E Stack object, possibly empty, from term tree.
                def entries := parseOptStack(optStack)
                
                for m in messages {
                    
                    def target := msgMap[m].getTarget()
                    
                    def edge := graph.makeEventArc(origin, target,
                                                   st, entries)
                    edge.setInTurn(inTurn)
                    
                    # If Resolved event has timestamp, it belongs to the edge.
                    if (optTimestamp =~ [timestamp]) {
                        edge.setOptTimestamp(timestamp)
                    }
                    if (comment =~ [text]) {
                        edge.setOptText(text)
                    }
                    edge.addLogFile(span)
                }
            }
        }
    }
    
    def roots := graph.getRoots()
    for r in roots {
        if (r != top) {
            graph.makeEventArc(top, r, "", [])
        }
    }
    pathnames := pathnames.sort()
        
    return [graph, top, pathnames.snapshot()]
}
