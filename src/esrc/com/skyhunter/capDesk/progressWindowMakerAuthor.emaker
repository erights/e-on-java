# Copyright 2002 Combex, Inc. under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

def progressWindowMakerAuthor(<swing>, standardWindow) :near {
    def progressWindowMaker(title) :near {
        var fileSize := 1000000000
        def progressWindow
        def myFrame := standardWindow(title,progressWindow)
        def myProgressPane := <swing:makeJProgressBar>()
        myProgressPane.setStringPainted(true)
        myFrame.getContentPane().add(myProgressPane)
        myFrame.pack()
        myFrame.setSize(150,50)
        #myFrame show()
        myFrame.setVisible(true)
        bind progressWindow {
            to setProgress(amountCopied) :void {
                def percent := (amountCopied *100) // fileSize
                myProgressPane.setValue(percent)
                myProgressPane.setString(`$percent%`)
            }
            to setFileSize(size) :void {
                fileSize := size
            }
            to windowClosing() :void {}
            to closeWindow() :void {
                myFrame.dispose()
            }
        }
    }
}
