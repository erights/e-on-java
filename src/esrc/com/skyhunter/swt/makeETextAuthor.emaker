# Copyright 2002 Combex, Inc. under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................


def <swt> := <import:org.eclipse.swt.*>
def SWT := <swt:makeSWT>
def <widget> := <swt:widgets.*>

/**
 * @param makeStyledText A function that makes StyledText widgets. The
 *                       function must take 2 arguments, parent and style
 */
def makeETextAuthor(makeStyledText, traceline) :near  {

    /**
     * makeEText creates a styledText with some crucial enhancements
     * Currently, it strips out cr chars before they get into
     * the widget, and it makes Ctrl-A do a selectAll.
     *
     */
    def makeEText(parent, style) :any {
        def textPane := makeStyledText(parent, style)

        #strip crs
        def verifyListener {
            to verifyText(event) :void {
                event.setText(event.getText().canonical())
            }
        }
        textPane.addVerifyListener(verifyListener)

        #make Select All Ctrl-A work
        def keyListener {
            to keyPressed(event) :void {
                if (event.getCharacter().asInteger() == 1 &&
                      event.getStateMask() == SWT.getCTRL()) {

                    textPane.setSelectionRange(0, textPane.getText().size())
                }
            }
            match _{}
        }
        textPane.addKeyListener(keyListener)

        textPane
    }
}
